""" The ego vehicle is driving on a straight road; the adversarial pedestrian suddenly crosses the road from the right front and suddenly stops in front of the ego.
"""
Town = globalParameters.town
EgoSpawnPt = globalParameters.spawnPt
yaw = globalParameters.yaw
egoTrajectory = PolylineRegion(globalParameters.waypoints)
param map = localPath(f'../maps/{Town}.xodr') 
param carla_map = Town
model scenic.simulators.carla.model
EGO_MODEL = "vehicle.lincoln.mkz_2017"

behavior AdvBehavior():
    speed = globalParameters.OPT_ADV_SPEED
    try:
        do CrossingBehavior(ego, speed, globalParameters.OPT_ADV_DISTANCE)
    interrupt when (distance from self to egoTrajectory) < speed * 0.1:
        take SetWalkingSpeedAction(0)
    interrupt when network.laneAt(ego) is not None and (distance from self to network.laneAt(ego).centerline) < speed * 0.1:
        take SetWalkingSpeedAction(0)

param OPT_GEO_X_DISTANCE = Range(0, 8)
param OPT_GEO_Y_DISTANCE = Range(10, 50)
param OPT_ADV_SPEED = Range(0, 5)
param OPT_ADV_DISTANCE = Range(0, 30)

ego = Car at EgoSpawnPt,
    with heading yaw,
    with regionContainedIn None,
    with blueprint EGO_MODEL
    
IntSpawnPt = OrientedPoint following roadDirection from EgoSpawnPt for globalParameters.OPT_GEO_Y_DISTANCE
AdvAgent = Pedestrian right of IntSpawnPt by globalParameters.OPT_GEO_X_DISTANCE,
    with heading IntSpawnPt.heading + 90 deg,
    with regionContainedIn None,
    with behavior AdvBehavior()

require (distance from AdvAgent to intersection) > 5